import os
import sys
import rtconfig

from rtconfig import RTT_ROOT

sys.path = sys.path + [os.path.join(RTT_ROOT, 'tools')]
from building import *

TARGET = 'rtthread.' + rtconfig.TARGET_EXT

DefaultEnvironment(tools=[])
env = Environment(tools = ['mingw'],
    AS = rtconfig.AS, ASFLAGS = rtconfig.AFLAGS,
    CC = rtconfig.CC, CCFLAGS = rtconfig.CFLAGS,
    CXX = rtconfig.CXX, CXXFLAGS = rtconfig.CXXFLAGS,
    AR = rtconfig.AR, ARFLAGS = '-rc',
    LINK = rtconfig.LINK, LINKFLAGS = rtconfig.LFLAGS)
env.PrependENVPath('PATH', rtconfig.EXEC_PATH)
env['ASCOM'] = env['ASPPCOM']

Export('RTT_ROOT')
Export('rtconfig')
rtconfig.CPU='riscv64'
rtconfig.ARCH='risc-v'

SDK_ROOT = os.path.abspath('../')

if os.path.exists(SDK_ROOT + '/libraries'):
    libraries_path_prefix = SDK_ROOT + '/libraries'
else:
    libraries_path_prefix = os.path.dirname(SDK_ROOT) + '/libraries'

# prepare building environment
objs = PrepareBuilding(env, RTT_ROOT, has_libcpu = False)

# include drivers
objs.extend(SConscript(os.path.join(libraries_path_prefix, 'driver', 'SConscript'), variant_dir='build/libraries/driver', duplicate=0))

objs.extend(SConscript(os.path.join(libraries_path_prefix, 'component', 'SConscript'), variant_dir='build/libraries/component', duplicate=0))

objs.extend(SConscript(os.path.join(libraries_path_prefix, 'applications', 'SConscript'), variant_dir='build/libraries/applications', duplicate=0))

stack_size = 4096

stack_lds = open('link_stacksize.lds', 'w')
if GetDepend('__STACKSIZE__'): stack_size = GetDepend('__STACKSIZE__')
stack_lds.write('__STACKSIZE__ = %d;\n' % stack_size)
stack_lds.close()

# make a building
DoBuilding(TARGET, objs)
